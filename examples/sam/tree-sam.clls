type rec treeInt {
    choice of {
        |#Leaf: send lint; close
        |#Node: send treeInt; treeInt
    }
};;

proc rec mapDouble(tin:~treeInt, tout:treeInt){
    case tin of {
        |#Leaf: #Leaf tout;
		recv tin (v);
		wait tin;
                send tout (2*v);
		close tout
        |#Node: recv tin(left:~treeInt);
                #Node tout;
                send tout(x:treeInt. mapDouble(left, x));
                mapDouble(tin,tout)
    }
};;



proc rec printAux(tin:~treeInt, x:close){
    case tin of {
        |#Leaf: recv tin(v);
		print("Leaf " + v);
		wait tin;
		close x
        |#Node: print("Node (");
                recv tin(left:~treeInt);
                cut{
                    printAux(left, y)
                    |y:wait|
                    wait y;
                    print(") (");
                    cut{
                        printAux(tin, z)
                        |z:wait|
                        wait z;
                        print(")");
                        close x
                    }
                }
    }
};;

proc printTree(tin:~treeInt){
    cut{
        printAux(tin, x)
        |x:wait|
        wait x;
        println(" ");
        ()
    }
};;

proc aTree(t:treeInt){
    #Node t;
    send t(tleft:treeInt. #Leaf tleft; send tleft (1); close tleft);
    #Leaf t; send t (2); close t
};;

proc aTree2(t:treeInt){
    #Node t;
    send t(tleft:treeInt. #Leaf tleft; send tleft (1); close tleft);
    #Node t;
    send t(tleft2:treeInt. #Leaf tleft2; send tleft2 (2); close tleft2);
    #Leaf t; send t (3); close t
};;

proc test1(){
    cut{
        aTree2(t)
        |t:~treeInt|
        printTree(t)
    }
};;

proc test2(){
    cut{
        aTree2(t)
        |t:~treeInt|
        mapDouble(t,t2)
        |t2:~treeInt|
        printTree(t2)
    }
};;

sam test1();;
sam test2();;
